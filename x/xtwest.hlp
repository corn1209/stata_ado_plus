{smcl}
{* 24june2010}{...}
{cmd:help xtwest}{right: ({browse "http://www.stata-journal.com/article.html?article=up0050":SJ16-1: st0146_1})}
{hline}

{title:Title}

{p2colset 5 15 17 2}{...}
{p2col :{hi:xtwest} {hline 2}}Westerlund error-correction-based panel cointegration tests{p_end}
{p2colreset}{...}


{title:Syntax}

{p 4 8 2}{cmd:xtwest}
{depvar}
{indepvars}
{ifin}{cmd:,} 
{cmd:lags(}{it:#} [{it:#}]{cmd:)}
{cmd:leads(}{it:#} [{it:#}]{cmd:)}
{cmd:lrwindow(}{it:#}{cmd:)}
[{cmd:constant}
{cmd:trend}
{cmd:bootstrap(}{it:#}{cmd:)}
{cmd:westerlund}
{cmd:noisily}
{cmd:mg}]

{p 4 4 2} {cmd:xtwest} is for use with panel data.  You must
{cmd:tsset} your data before using {cmd:xtwest}; see {manhelp tsset TS}.


{title:Description}

{p 4 4 2} {cmd:xtwest} implements the four panel cointegration tests developed
by Westerlund (2007). The underlying idea is to test for the absence of
cointegration by determining whether the individual panel members are error
correcting. Consider the following error-correction model:

{p 8 8 2} D.y_it = c_i + {bind:a_i1*D.y_it-1} + {bind:a_i2*D.y_it-2} + ... + {bind: a_ip*D.y_it-p} 

{p 16 8 2}  {space 4} + {bind:b_i0*D.x_it} + {bind:b_i1*D.x_it-1} + ... + {bind:b_ip*D.x_it-p} 

{p 16 8 2}  {space 4} + a_i({bind:y_it-1 - b_i*x_it-1})  +  u_it

{p 4 4 2} a_i provides an estimate of the speed of error-correction
toward the long run equilibrium {bind:y_it = - (b_i/a_i) * x_it}
for that series i. The Ga and Gt test statistics test H0: a_i = 0 for
all i versus H1: a_i < 0 for at least one i. These statistics start
from a weighted average of the individually estimated a_i's and their 
t-ratio's, respectively. The Pa and Pt test statistics pool information
over all the cross-sectional units to test H0: a_i = 0 for all i versus H1: 
a_i < 0 for all i. Rejection of H0 should therefore be taken as rejection
of cointegration for the panel as a whole.

{p 4 4 2} The tests are very flexible and allow for an almost completely
heterogeneous specification of both the long- and short-run parts of the 
error-correction model, where the latter can be determined from the data. The
series are allowed to be of unequal length. 

{p 4 4 2} If the cross-sectional units are suspected of being correlated,
robust critical values can be obtained through bootstrapping.


{title:Options}

{p 4 8 2}{cmd:lags(}{it:#} [{it:#}]{cmd:)} specifies the number of lags to be
included in the error-correction equations.  If one number is specified, it
determines a fixed number of lags, p. If two numbers are specified, the
Akaike information criterion (AIC) is used to determine an optimal lag
length, p_i, for each separate time series, within the given limits.

{p 4 8 2}{cmd:leads(}{it:#} [{it:#}]{cmd:)} specifies the number of 
leads to be included in the error-correction equations; this is similar to the
{cmd:lags()} option.

{p 4 8 2}{cmd:lrwindow(}{it:#}{cmd:)}  sets the width of the Bartlett kernel
window used in the semiparametric estimation of long-run variances.

{p 4 8 2}{cmd:constant} adds a constant to the cointegration
relationship. 

{p 4 8 2}{cmd:trend} allows for a deterministic trend in the cointegration
relationship.

{p 4 8 2}{cmd:bootstrap(}{it:#}{cmd:)} shows bootstrapped p-values
for all four test statistics.  These are robust in the presence of common
factors in the time series. The argument determines the number of bootstrap
replications. On Stata/IC, the number of replications must be smaller than 800.

{p 4 8 2}{cmd:westerlund} replicates the tables in Westerlund (2007).

{p 4 8 2}{cmd:noisily} shows the regressions for the
separate series.  If a range of lags or leads is given, only the regression
chosen by the AIC is shown.

{p 4 8 2}{cmd:mg} reports the mean-group error-correction model, averaging
coefficients of the error-correction equation over all cross-sectional units,
together with the implied long-run relationship.  This is much like the output
of {helpb xtpmg} with the {cmd:mg} option, but here it allows for different
lag and lead lengths in the different cross-sectional units.


{title:Examples}

{phang2}{bf:. {stata "use xtwestdata"}}{p_end}

{p 4 4 2}A constant term is added to the cointegration equation{p_end}
{phang2}{bf:. {stata "xtwest loghex loggdp, constant lags(1 3) leads(0 3) lrwindow(3)"}}{p_end}

{p 4 4 2}Bootstrapping takes a long time unless the lead and lag lengths are fixed{p_end}
{phang2}{bf:. {stata "xtwest loghex loggdp, constant trend lags(1) leads(1) lrwindow(3) bootstrap(100)"}}{p_end}

{p 4 4 2}The {cmd:westerlund} option should be used only to replicate the results of Westerlund (2007){p_end}
{phang2}{bf:. {stata "xtwest loghex loggdp, westerlund constant trend lags(1 3) leads(0 3) lrwindow(3)"}}{p_end}
{phang2}{bf:. {stata "xtwest loghex loggdp, westerlund constant trend lags(1) leads(1) lrwindow(3) bootstrap(100)"}}{p_end}


{title:Reference}

{p 4 8 2} Westerlund, J. 2007. Testing for error correction in panel data.
{it:Oxford Bulletin of Economics and Statistics} 69: 709-748. {p_end}


{title:Authors}

{p 4}Damiaan Persyn (corresponding author){p_end}
{p 4}LICOS, Centre for Institutions and Economic Performance {p_end}
{p 4}Katholieke Universiteit Leuven{p_end}
{p 4}Leuven, Belgium{p_end}
{p 4}damiaan.persyn@econ.kuleuven.be{p_end}

{p 4}Joakim Westerlund{p_end}
{p 4}Department of Economics{p_end}
{p 4}Lund University{p_end}
{p 4}Lund, Sweden{p_end}
{p 4}joakim.westerlund@nek.lu.se{p_end}


{title:Also see}

{p 4 14 2}
Article:  {it:Stata Journal}, volume 16, number 1: {browse "http://www.stata-journal.com/article.html?article=up0050":st0146_1},{break}
         {it:Stata Journal}, volume 8, number 2: {browse "http://www.stata-journal.com/article.html?article=st0146":st0146}

{p 7 14 2}
Help:  {helpb ipshin}, {helpb hadrilm}, {helpb xtfisher}, {helpb xtpmg} (if installed)
