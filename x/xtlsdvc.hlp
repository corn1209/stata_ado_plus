{smcl}
{* 31mar2005}{...}
{hline}
help for {hi:xtlsdvc}{right:(SJ5-4: st0091)}
{hline}

{title: Bias corrected LSDV dynamic panel data estimator}

{p 8 16 2}{cmd:xtlsdvc}
{it:depvar}
[{it:indepvars}] [{it:if}]{cmd:,}
{cmdab:i:nitial:(}{it:estimator}{cmd:)}
[{cmdab:l:evel:(}{it:#}{cmd:)}
{cmdab:b:ias:(}{it:#}{cmd:)}
{cmdab:v:cov:(}{it:#}{cmd:)}
{cmdab:f:irst}
{cmdab:l:sdv}]

{p 4 4 2}
where {it:estimator} is

{p 12 36 2}{cmd:ah}{space 18}Anderson-Hsiao{p_end}
{p 12 36 2}{cmd:ab}{space 18}Arellano-Bond{p_end}
{p 12 36 2}{cmd:bb}{space 18}Blundell-Bond{p_end}
{p 12 36 2}{cmd:my}{space 18}initial values supplied by the user{p_end}

{p 4 4 2}
{cmd:xtlsdvc} is for use with time-series data.
You must {cmd:tsset} your data before using {cmd:xtlsdvc}; see
{helpb tsset}. However, since {cmd:xtlsdvc} calls {cmd:xtreg}, {it:indepvars} 
may not contain time-series operators; see {helpb xtreg}.

{p 4 4 2}
{cmd:xtlsdvc} shares the features of all estimation commands; see
{help estcom}.


{p 4 4 2}
The syntax of {help predict} following {cmd:xtlsdvc} is

{p 8 16 2}{cmd:predict} [{it:type}] {it:newvarname}
{ifin} [{cmd:,} {it:statistic}]

{p 4 4 2}
where y[i,t] = {bind: y[i,t-1]a} + {bind:x[i,t]b} + u[i] + e[i,t]
and {it:statistic} is

{p 8 21 2}{cmd:xb} {space 4} y[i,t-1]a + x[i,t]b, fitted values; the default{p_end}
{p 8 21 2}{cmd:ue} {space 4} u[i] + e[i,t], the combined residual{p_end}
{p 4 21 2}(*) {cmd:xbu} {space 3} y[i,t-1]a + x[i,t]b + u[i], prediction, including fixed effect{p_end}
{p 4 21 2}(*) {cmd:u} {space 5} u[i], the fixed effect{p_end}
{p 4 21 2}(*) {cmd:e} {space 5} e[i,t], the observation-specific error
component{p_end}

{p 4 4 2}
Unstarred statistics are available both in and out of sample; type
{cmd:predict} ... {cmd:if e(sample)} ... if wanted only for the
estimation sample.  Starred statistics are calculated only for the estimation
sample, even when {cmd:if e(sample)} is not specified.


{title:Description}

{p 4 4 2}
{cmd:xtlsdvc} calculates bias-corrected least-squares dummy variable (LSDV)
estimators for the standard autoregressive panel-data model using the bias
approximations in Bruno (2005a), who extends the results by Bun and Kiviet
(2003), Kiviet (1999), and Kiviet (1995) to unbalanced panels

{p 4 12 2}y[i,t] = {bind: y[i,t-1]a} + {bind:x[i,t]b} + u[i] + e[i,t]
{space 4} i={c -(}1,...,N{c )-}; {space 3} t={c -(}1,...,T_i{c )-},

    where

{p 4 12 2}a is a parameter to be estimated{p_end}

{p 4 12 2}x[i,t] is a (1 X (k-1)) vector of strictly exogenous covariates{p_end}

{p 4 12 2}b is a ((k-1) X 1) vector of parameters to be estimated{p_end}

{p 4 12 2}u[i] are the individual effects, for which no distributional assumption is made
apart being fixed over time,{p_end}

{p 4 12 2}and e[i,t] are iid over the whole sample with variance s_e*s_e.{p_end}

{p 4 4 2}
It is also assumed that the u[i] and the e[i,t] are independent for each
i over all t.

{p 4 4 2}
A more detailed description of {cmd:xtlsdvc} can be found in Bruno (2005b).


{title:Options}

{p 4 8 2}{cmd:initial(}{it:estimator}{cmd:)} is required and specifies the
consistent estimator chosen to initialize the bias correction.

{synoptset 11 tabbed}{...}
{synopt :{it:estimator}}description{p_end}
{synoptline}
{synopt :{opt ah}}AH estimator, with the dependent variable lagged two times,
   used as an instrument for the first-differenced model with no intercept
   ({helpb ivreg}){p_end}
{synopt :{opt ab}}standard one-step AB estimator with no intercept
   ({helpb xtabond}){p_end}
{synopt :{opt bb}}standard BB estimator with no intercept, as implemented by
   the user-written Stata routine {cmd:xtabond2} by Roodman (2003){p_end}
{synopt :{opt my}}a row vector of initial values supplied directly by the
user{p_end}
{synoptline}
{p2colreset}{...}

{p 8 8 2}
To implement the last instance of this option, the user must create a 
{1 x (k+1)} matrix to be named {cmd:my}, the i element of which
serves as an initial value for the coefficient on the i variable in
{it:varlist} and the last, (k+1), element as an estimate for the error
variance. This may be useful in Monte Carlo simulations or if the user wishes
to try initial estimators other than {cmd:ah}, {cmd:ab}, or {cmd:bb}.  

{p 4 8 2}{cmd:level(}{it:#}{cmd:)} specifies the confidence level, as a
percentage, for confidence intervals of the coefficients. The default is
{cmd:level(95)} or as set by {helpb set level}.

{p 4 8 2}{cmd:bias(}{it:#}{cmd:)} determines the accuracy of the approximation: 
{it:#}=1 (default) forces an approximation up to O(1/T); {it:#}=2 forces
and approximation up to O(1/NT); {it:#}=3 forces an approximation up to
O(N^{-1}T^{-2}).

{p 4 8 2}{cmd:vcov(}{it:#}{cmd:)} calculates a bootstrap variance-covariance
matrix for LSDVC using {it:#} repetitions ({it:#} may not equal 1). Normality
for errors is assumed.  This procedure continues to work also in the presence
of gaps in the exogenous variables, although in this case, bootstrap samples
for each unit are truncated to the first missing value encountered. Gaps in
the dependent variable, instead, bear no consequence to the bootstrap sample
size. 

{p 4 8 2}{cmd:first} requests that the first-stage regression results be
displayed.

{p 4 8 2}{cmd:lsdv} requests that the original LSDV regression results be
displayed.


{title:Options for predict}

{p 4 8 2}{cmd:xb} calculates the linear prediction; that is, y[i,t-1]a +
x[i,t]b.  This is the default. 

{p 4 8 2}{cmd:ue} calculates the prediction of u[i] + e[i,t].

{p 4 8 2}{cmd:xbu} calculates the prediction of y[i,t-1]a + x[i,t]b + u[i], the
prediction including the fixed component.

{p 4 8 2}{cmd:u} calculates the prediction of u[i], the estimated fixed effect.

{p 4 8 2}{cmd:e} calculates the prediction of e[i,t].


{title:Remarks}

{p 4 4 2}{cmd:xtlsdvc} does not report analytical standard errors. Only
bootstrap standard errors are reported, provided that {cmd:vcov(}{it:#}{cmd:)}
is given. 

{p 4 4 2}Bootstrap standard errors are downward biased when values for the
unknown parameters are supplied through the matrix {cmd:my}, since the
procedure, keeping {cmd:my} fixed over replications, neglects a source of
variability of the bias-corrected LSDV estimator.  


{title:Saved results}

{p 4 4 2}{cmd:xtlsdvc} saves in {cmd:e()}:

{col 5}Scalars
{col 9}{cmd:e(N)}{col 23}number of observations
{col 9}{cmd:e(Tbar)}{col 23}average number of time periods
{col 9}{cmd:e(sigma)}{col 23}estimates of sigma through the within 
{col 23}residuals from the first-stage regression 
{col 9}{cmd:e(N_g)}{col 23}number of groups

{col 5}Macros
{col 9}{cmd:e(cmd)}{col 23}{cmd:xtlsdvc}
{col 9}{cmd:e(ivar)}{col 23}panel variable
{col 9}{cmd:e(depvar)}{col 23}name of dependent variable
{col 9}{cmd:e(predict)}{col 23}program used to implement {cmd:predict}

{col 5}Matrices
{col 9}{cmd:e(b)}{col 23}{cmd:xtlsdvc} estimate
{col 9}{cmd:e(b_lsdv)}{col 23}coefficient vector of the uncorrected LSDV
{col 9}{cmd:e(V_lsdv)}{col 23}variance-covariance matrix of the uncorrected LSDV
{col 9}{cmd:e(V)}{col 23}variance-covariance matrix of the estimators

{col 5}Functions
{col 9}{cmd:e(sample)}{col 23}marks estimation sample


{title:Examples}

{p 4 8 2}{cmd:. xtlsdvc n w k ys yr1980-yr1984, initial(ah)}{p_end}
{p 4 8 2}{cmd:. xtlsdvc n w k ys yr1980-yr1984, initial(ab) bias(3)}{p_end}
{p 4 8 2}{cmd:. xtlsdvc n w k ys yr1980-yr1984, initial(ab) bias(3) vcov(50)}{p_end}


{title:References}

{p 4 8 2}Bruno, G. S. F. 2005a.
Approximating the bias of the LSDV estimator for
dynamic unbalanced panel data models.
{it:Economics Letters} 87: 361-366.

{p 4 8 2}------. 2005b.
Estimation and inference in dynamic unbalanced panel data
models with a small number of individuals.
{it:CESPRI WP n.165}. Università Bocconi-CESPRI, Milan.

{p 4 8 2}Bun, M. J. G. and J. F. Kiviet. 2003. On the diminishing
returns of higher order terms in asymptotic expansions of bias.
{it:Economics Letters} 79: 145-152.

{p 4 8 2}Kiviet, J. F. 1995. On bias, inconsistency, and
efficiency of various estimators in dynamic panel data models.
{it:Journal of Econometrics} 68: 53-78.

{p 4 8 2}------. 1999. Expectation of expansions for estimators
in a dynamic panel data model; some results for weakly exogenous Regressors.
In {it:Analysis of Panel Data and Limited Dependent Variables},
ed. c. Hsiao, K. Lahiri, L.-F. Lee, and M. H. Pesaran, 199-225. 
Cambridge: Cambridge University Press.

{p 4 8 2}Roodman, D. M. 2003. XTABOND2: Stata module to extend xtabond
dynamic panel-data estimator.  Statistical Software Components S435901, Boston
College Department of Economics.


{title:Author}

{p 4}Giovanni S.F. Bruno{p_end}
{p 4}Istituto di Economia Politica, Università Bocconi{p_end}
{p 4}Milan, Italy{p_end}
{p 4}giovanni.bruno@unibocconi.it{p_end}


{title:Also see}

{p 4 12 2}
Manual: {hi:[U] 23 Estimation and post-estimation commands},{break}
{hi:[U] 29 Overview of Stata estimation commands},{break}
{hi:[XT] xtabond},
{hi:[XT] xtivreg},
{hi:[R] ivreg}

{p 4 13 2}
Online:  {help estcom}, {helpb ivreg}, {help postest}, 
{helpb xtabond}, {helpb xtdes}, {helpb xtivreg}, {helpb xtreg}, {helpb xtregar},
{helpb xtsum}
{p_end}
