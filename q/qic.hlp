{smcl}
{* *! version 1.1.5  20 December 2007}{...}
{hi:help qic}{right: ({browse "http://www.stata-journal.com/article.html?article=st0126_1":SJ8-1: st0126_1}; {browse "http://www.stata-journal.com/article.html?article=st0126":SJ7-2: st0126})}
{hline}

{title:Title}

{p2colset 5 12 14 2}{...}
{p2col :{hi:qic} {hline 2}}QIC criterion for model selection in GEE analyses{p_end}
{p2colreset}{...}


{title:Syntax}

{p 8 14 2}
{cmd:qic} {depvar} [{indepvars}] {ifin} [{cmd:,} {it:options}]

{synoptset 18}{...}
{synopthdr}
{synoptline}
{synopt :{opth "i(varname:varname_i)"}}use {it:varname_i} as the panel ID variable{p_end}
{synopt :{opth "t(varname:varname_t)"}}use {it:varname_t} as the time variable{p_end}
{synopt :{cmdab:f:amily(}{it:{help qic##family:family}}{cmd:)}}distribution of {depvar}{p_end}
{synopt :{cmdab:l:ink(}{it:{help qic##link:link}}{cmd:)}}link function{p_end}

{synopt :{opth e:xposure(varname)}}include ln({it:varname}) in model with coefficient constrained to 1{p_end}
{synopt :{opth off:set(varname)}}include {it:varname} in model with coefficient constrained to 1{p_end}
{synopt :{opt nocon:stant}}suppress constant term{p_end}
{synopt :{opt force}}estimate even if observations unequally spaced in time{p_end}

{synopt :{cmdab:c:orr(}{it:{help qic##correlation:correlation}}{cmd:)}}within-group correlation structure{p_end}

{synopt :{opt r:obust}}use Huber/White/sandwich estimator of variance{p_end}
{synopt :{opt nmp}}use divisor N-P instead of the default N{p_end}
{synopt :{opt rgf}}multiply the robust variance estimate by (N-1)/(N-P){p_end}
{synopt :{cmdab:s:cale(x2)}}set scale parameter to Pearson chi-squared statistic{p_end}
{synopt :{cmdab:s:cale(dev)}}set scale parameter to deviance divided by degrees of freedom{p_end}
{synopt :{cmdab:s:cale(phi)}}do not rescale the variance{p_end}
{synopt :{opt scale(#)}}set scale parameter to {it:#}{p_end}

{synopt :{opt lev:el(#)}}set confidence level; default is {cmd:level(95)}{p_end}
{synopt :{opt ef:orm}}report exponentiated coefficients{p_end}

{synopt :{it:{help qic##optimize_options:optimize_options}}}control the optimization process; seldom used{p_end}

{p2coldent :{opt nodis:play}}suppress display of header and coefficients{p_end}
{synoptline}
{p2colreset}{...}
{p 4 6 2}{it:depvar} and {it:indepvars} may contain time-series operators; see {help tsvarlist}.{p_end}
{p 4 6 2} {opt iweight}s, {opt fweight}s, and {opt pweight}s are allowed; see
{help weight}.  Weights must be constant within panel.{p_end}

{marker family}{...}
{synoptset 23}{...}
{synopthdr :family}
{synoptline}
{synopt :{opt gau:ssian}}Gaussian (normal); {cmd:family({opt nor:mal})} is a synonym{p_end}
{synopt :{opt ig:aussian}}inverse Gaussian{p_end}
{synopt :{opt b:inomial}}Bernoulli/binomial (k=1){p_end}
{synopt :{opt po:isson}}Poisson{p_end}
{synopt :{opt nb:inomial}}negative binomial (k=1){p_end}
{synopt :{opt gam:ma}}gamma{p_end}
{synoptline}
{p2colreset}{...}

{marker link}{...}
{synoptset 23}{...}
{synopthdr :link}
{synoptline}
{synopt :{opt i:dentity}}identity; y=y{p_end}
{synopt :{opt log}}log; ln(y){p_end}
{synopt :{opt logi:t}}logit; ln{y/(1-y)}, natural log of the odds{p_end}
{synopt :{opt p:robit}}probit; inverse Gaussian cumulative{p_end}
{synopt :{opt cl:oglog}}cloglog; ln{-ln(1-y)}{p_end}
{synopt :{opt pow:er}[{it:#}]}power; y^k with k={it:#}; {it:#}=1 if not specified{p_end}
{synopt :{opt opo:wer}[{it:#}]}odds power; [{y/(1-y)}^k - 1]/k with k=#; #=1 if not specified{p_end}
{synopt :{opt nb:inomial}}negative binomial{p_end}
{synopt :{opt rec:iprocal}}reciprocal; 1/y{p_end}
{synoptline}
{p2colreset}{...}

{marker correlation}{...}
{synoptset 23}{...}
{synopthdr :correlation}
{synoptline}
{synopt :{opt exc:hangeable}}exchangeable{p_end}
{synopt :{opt ind:ependent}}independent{p_end}
{synopt :{opt uns:tructured}}unstructured{p_end}
{synopt :{opt fix:ed} {it:matname}}user-specified{p_end}
{synopt :{opt ar} {it:#}}autoregressive of order {it:#}{p_end}
{synopt :{opt sta:tionary} {it:#}}stationary of order {it:#}{p_end}
{synopt :{opt non:stationary} {it:#}}nonstationary of order {it:#}{p_end}
{synoptline}
{p2colreset}{...}


{title:Description}

{pstd}
{cmd:qic} calculates the QIC and QIC_u criteria for model selection in GEE,
which is an extension of the widely used AIC criterion in ordinary regression
(Pan 2001).  It allows for specification of all seven distributions -- Gaussian,
inverse Gaussian, Bernoulli/binomial, Poisson, negative binomial, and gamma --
all link functions and working correlation structures, and all standard
error/robust options (except for the {cmd:vce()} option) available in Stata
9.0.  It also calculates the trace of the matrix O^{-1}V, where O is the
variance estimate under the independent correlation structure, and V is the
variance estimate under the specified working correlation structure in GEE.
When trace is close to the number of parameter p, the QIC_u is a good
approximation to QIC.


{title:Options}

{phang}
{opth "i(varname:varname_i)"} specifies {it:varname_i} as the panel ID
variable.

{phang}
{opth "t(varname:varname_t)"} specifies {it:varname_t} as the time variable.
{cmd:qic} does not need to know {opt t()} for the {cmd:corr(independent)}
and {cmd:corr(exchangeable)} correlation structures.  Whether you specify 
{opt t()} makes no difference in these two cases.

{phang}
{opt family(family)} specifies the distribution of {depvar}.
{cmd:family(gaussian)} is the default.

{phang}
{opt link(link)} specifies the link function. The default is
the canonical link for the specified {opt family()}.

{phang}
{opth exposure(varname)} and {opth offset(varname)} are different ways of
specifying the same thing.  {opt exposure()} specifies a variable that reflects
the amount of exposure over which the {depvar} events were observed for each
observation; ln({it:varname}) with coefficient constrained to be 1 is entered
into the regression equation.  {opt offset()} specifies a variable that is to
be entered directly into the log-link function with its coefficient
constrained to be 1; thus, exposure is assumed to be e^varname.  If you were
fitting a Poisson regression model, {cmd:family(poisson) link(log)}, for
instance, you would account for exposure time by specifying {opt offset()}
containing the log of exposure time.

{phang}
{opt noconstant} specifies that the linear predictor has no intercept term and
thus forces it to pass through the origin on the scale defined by the link
function.

{phang}
{opt force} specifies that estimation be forced even though {opt t()} is not
equally spaced.  This option is relevant only for correlation structures that
require {opt t()} and observations be equally spaced.

{phang}
{opt corr(correlation)} specifies the within-subject correlation structure.  The default is the exchangeable correlation structure {cmd:corr(exchangeable)}.

{phang}
{opt robust} specifies that the Huber/White/sandwich estimator of variance
be used in place of the default GLS variance estimator.
This produces valid standard errors even if the correlations
within group are not as hypothesized by the specified correlation structure.
It does, however, require that the model correctly specify the mean. 
The resulting standard errors are thus labeled "Semi-Robust" instead of
"Robust".  Although there is no {opt cluster()} option, results are
as if there were a {opt cluster()} option and you specified clustering on 
{opt i()}.

{phang}
{opt nmp} specifies that the divisor N-P be used instead of the default N,
where N is the total number of observations, and P is the number of
coefficients estimated.

{phang}
{opt rgf} specifies that the robust variance estimate be multiplied by
(N-1)/(N-P), where N = {it:#} of observations and P = {it:#} of coefficients
estimated.  This option can be used only with {cmd:family(gaussian)} when
{opt robust} is either specified or implied by the use of {opt pweight}s.
Using this option implies that the robust variance estimate is not invariant
to the scale of any weights used.

{phang}
{cmd:scale(x2}|{cmd:dev}|{cmd:phi}|{it:#}{cmd:)} overrides the default scale
parameter.  By default, {cmd:scale(1)} is assumed for the discrete
distributions (binomial, negative binomial, and Poisson), and {cmd:scale(x2)}
is assumed for the continuous distributions (gamma, Gaussian, and inverse
Gaussian).

{pmore}
{cmd:scale(x2)} specifies that the scale parameter be set to the Pearson
chi-squared (or generalized chi-squared) statistic divided by the residual
degrees of freedom, which is recommended by McCullagh and Nelder as a
good general choice for continuous distributions.

{pmore}
{cmd:scale(dev)} sets the scale parameter to the deviance divided by the
residual degrees of freedom. This option provides an alternative to
{cmd:scale(x2)} for continuous distributions and for over- or underdispersed
discrete distributions.

{pmore}
{cmd:scale(phi)} specifies that the scale parameter be estimated from the data.
{cmd:xtgee}'s default scaling makes results agree with other estimators and
has been recommended by McCullagh and Nelder in the context of
GLM.  When comparing results with calculations made by other software,
you may find that the other packages do not offer this feature.  In such
cases, specifying {cmd:scale(phi)} should match their results.

{pmore}
{opt scale(#)} sets the scale parameter to {it:#}. For example,
using {cmd:scale(1)} in {cmd:family(gamma)} models results in
exponential-errors regression (if you use assume independent correlation
structure).

{phang}
{opt level(#)} specifies the confidence level, as a percentage, for confidence
intervals.  The default is {cmd:level(95)} or as set by {helpb set level}.

{phang}
{opt eform} displays the exponentiated coefficients and corresponding standard
errors and confidence intervals as described in {helpb maximize}.  For
{cmd:family(binomial) link(logit)} (i.e., logistic regression), exponentiation
results in odds ratios; for {cmd:family(poisson) link(log)} (i.e., Poisson
regression), exponentiated coefficients are incidence-rate ratios.

{phang}
{marker optimize_options}
{it:optimize_options} control the iterative optimization process.  These
options are seldom used.

{pmore}
{opt iter:ate(#)} specifies the maximum number of iterations.  When the number 
of iterations equals {it:#}, the optimization stops and presents the current
results, even if the convergence tolerance has not been reached.  The default
value of {opt iterate()} is 100.

{pmore}
{opt tol:erance(#)} specifies the tolerance for the coefficient vector.  When 
the relative change in the coefficient vector from one iteration to the next is
less than or equal to {it:#}, the optimization process is stopped.  
{cmd:tolerance(1e-6)} is the default.

{pmore}
{opt nolog} suppresses the display of the iteration log.

{pmore}
{opt tr:ace} specifies that the current estimates are printed at each
iteration.

{phang}
{opt nodisplay} suppresses the display of the header and coefficients.


{title:Examples 1}

{phang}. {stata "use http://www.stata-press.com/data/r9/nlswork2, clear":use http://www.stata-press.com/data/r9/nlswork2, clear} 

{phang}. {stata iis id}

{phang}. {stata qic ln_w grade age if race == 2}

{phang}. {stata qic ln_w grade age, t(year) corr(uns) scale(dev) force nolog nodis trace}

{phang}. {stata qic ln_w grade age, t(year) corr(exc) force}


{title:Examples 2}

{phang}. {stata "use http://www.stata-press.com/data/r9/union, clear":use http://www.stata-press.com/data/r9/union, clear}
 
{phang}. {stata iis idcode}

{phang}. {stata tis year}

{phang}. {stata qic union age grade not_smsa south if black == 1, fam(bin)}

{phang}. {stata qic union age grade not_smsa south, fam(bin) link(probit) corr(uns) force tol(1e-8) iter(20)}

{phang}. {stata qic union age grade not_smsa south, fam(bin) link(cloglog) corr(ar) force scale(x2)}


{title:Examples 3}

{phang}. {stata "use http://www.stata-press.com/data/r9/ships, clear":use http://www.stata-press.com/data/r9/ships, clear} 

{phang}. {stata egen wave = group(yr_con yr_op)}

{phang}. {stata iis ship}

{phang}. {stata tis wave}

{phang}. {stata qic accident op_75_79 co_65_69 co_70_74 co_75_79 if wave <= 6, fam(poi) corr(exc) ex(service)}

{phang}. {stata qic accident op_75_79 co_65_69 co_70_74 co_75_79, fam(poi) corr(sta) ex(service) force tol(1e-10) scale(dev)}

{phang}. {stata qic accident op_75_79 co_65_69 co_70_74 co_75_79, fam(poi) corr(exc) ex(service) force nodis}


{title:Examples 4}

{phang}. {stata "use http://www.stata-press.com/data/r9/airacc, clear":use http://www.stata-press.com/data/r9/airacc, clear} 

{phang}. {stata iis airline}

{phang}. {stata tis time}

{phang}. {stata qic i_cnt inprog if airline <= 15, fam(nb 2) corr(exc) exposure(pmiles)}

{phang}. {stata qic i_cnt inprog, fam(nb 2) corr(sta) exposure(pmiles) force tol(1e-8) nodis}

{phang}. {stata qic i_cnt inprog, fam(nb 2) corr(uns) exposure(pmiles) force scale(x2)}

{phang}. {stata qic i_cnt inprog, fam(gam) corr(sta) exposure(pmiles) force scale(dev)}

{phang}. {stata qic i_cnt inprog, fam(ig) corr(uns) exposure(pmiles) force}


{title:References}

{phang}
Cui, J. 2007. QIC program and model selection in GEE analyses.
{browse "http://www.stata-journal.com/article.html?article=st0126":{it:Stata Journal} 7: 209-220}.

{phang}
Cui, J., and G. Qian. 2007. Selection of working correlation structure and
best model in GEE analyses of longitudinal data.
{it:Communications in Statistics: Simulation and Computation} 36:
987-996.

{phang}
Pan, W. 2001. Akaike's information criterion in generalized estimating
equations. {it:Biometrics} 57: 120-125.{p_end}


{title:Author}

{p 4 4 2}
James Cui, Department of Epidemiology and Preventive Medicine, Monash University.

{p 4 4 2}
Email: {browse "mailto:james.cui@med.monash.edu.au":james.cui@med.monash.edu.au}


    Other commands I have written:
    {helpb genhwcci}   (if installed)  {stata ssc install genhwcci}   (to install this command)
    {helpb simuped2}   (if installed)  {stata ssc install simuped2}   (to install this command)
    {helpb simuped3}   (if installed)  {stata ssc install simuped3}   (to install this command)
    {helpb phenotype}  (if installed)  {stata ssc install phenotype}  (to install this command)
    {helpb buckley}    (if installed)  {stata ssc install buckley}    (to install this command)


{title:Also see}

{psee}
Article: {it:Stata Journal}, volume 8, number 1: {browse "http://www.stata-journal.com/article.html?article=st0126_1":st0126_1}{break}
         {it:Stata Journal}, volume 7, number 2: {browse "http://www.stata-journal.com/article.html?article=st0126":st0126}

{psee}
Manual:  {bf:[XT] xtgee}

{psee}
Online:  {manhelp xtgee_postestimation XT:xtgee postestimation};{break}
{manhelp xtlogit XT}, {manhelp xtnbreg XT},  {manhelp xtpoisson XT},
{manhelp xtprobit XT}, {manhelp xtreg XT}, {manhelp xtregar XT},
{manhelp glm R}, {manhelp logistic R}, {manhelp regress R}
{p_end}
