{smcl}
{* 3nov2004/9oct2007/18dec2009}{...}
{hline}
{cmd:help rvfplot2}{right:({browse "http://www.stata-journal.com/sjpdf.html?articlenum=up0027":SJ10-1: gr0009_1})}
{hline}

{title:Title}

{p 4 4 2}{hi:rvfplot2} {hline 2} Plot residuals versus fitted values after model fit


{title:Syntax}

{p 8 17 2}{cmd:rvfplot2} [{cmd:,} {it:residualtype} {it:qualifier} 
{cmdab:rsc:ale(}{it:exp}{cmd:)}
{cmdab:fsc:ale(}{it:exp}{cmd:)}
{cmd:lowess}[{cmd:(}{it:lowess_options}{cmd:)}] 
{cmd:rcspline}[{cmd:(}{it:rcspline_options}{cmd:)}]
{it:scatter_options}
[{cmd:addplot(}{it:plot}{cmd:)}
{c |}
{cmd:plot(}{it:plot}{cmd:)}]
] 


{title:Description}

{p 4 4 2}{cmd:rvfplot2} plots residuals versus fitted values.  The
residuals are, by default, those calculated by {cmd:predict, residuals} or (if
the previous estimation command was {cmd:glm}) by {cmd: predict, response}.
The fitted values are those produced by {cmd:predict} by default after each
estimation command.

{p 4 4 2}{cmd:rvfplot2} is for use after {cmd:regress} and similar commands;
see the help file for the command of interest. It is a generalization of
{cmd:rvfplot} in official Stata. 


{title:Options}

{p 4 8 2}{it:residualtype} specifies a type of residual other than the default.
The following types are currently supported: {cmdab:a:nscombe},
{cmdab:d:eviance}, {cmdab:l:ikelihood}, {cmdab:p:earson}, {cmdab:r:esiduals},
{cmdab:resp:onse}, {cmdab:rsta:ndard}, {cmdab:rstu:dent}, {cmdab:s:core},
{cmdab:w:orking}. 

{p 4 8 2}{it:qualifier} specifies one of {cmdab:sta:ndardized}, 
{cmdab:stu:dentized}, {cmdab:mod:ified}, or {cmdab:adj:usted}. 

{p 4 8 2}{cmd:rscale(}{it:exp}{cmd:)} specifies a transformed scale on which to
show the residuals using Stata syntax and {cmd:X} as a placeholder for the
residual variable name. Thus {cmd:rscale(X^2)} specifies squaring, to show
relative contribution to residual variance; {cmd:rscale(abs(X))} specifies
absolute value, to set aside sign; {cmd:rscale(sqrt(abs(X)))} specifies root
of absolute value, a useful scale on which to check for heteroskedasticity.

{p 4 8 2}{cmd:fscale(}{it:exp}{cmd:)} specifies a transformed scale on which to
show the fitted values using Stata syntax and {cmd:X} as a placeholder for the
fitted variable name. For example, {cmd:fscale(2 * ln(X))} specifies twice 
the natural logarithm, which is the constant information scale for a 
generalized linear model with gamma error. Similarly, arguments 
of {cmd:2 * sqrt(X)}, {cmd:2 * asin(sqrt(X))}, and {cmd:-2 / sqrt(X)} specify
the constant information scale for Poisson, binomial, and inverse Gaussian
errors, respectively. See McCullagh and Nelder (1989, 398) for background. 

{p 4 8 2}{cmd:lowess}[{cmd:(}{it:lowess_options}{cmd:)}] specifies that the
residuals 
will be smoothed as a function of the fitted using {helpb twoway lowess}.  
{cmd:lowess} may also be specified with options for {helpb twoway lowess}. 

{p 4 8 2}{cmd:rcspline}[{cmd:(}{it:rcspline_options}{cmd:)}] (Stata 10 and
later) specifies that the residuals will be smoothed as a 
function of the fitted using restricted cubic splines. 
{cmd:rcspline} may also be specified with either 
the {cmd:nknots()} or {cmd:knots()} options for {helpb mkspline} or else 
graphics options for {helpb twoway mspline}.  

{p 4 8 2} 
{it:scatter_options} affect the rendition of the plotted points; see
{helpb scatter}.

{p 4 8 2}
{opth "addplot(addplot_option:plot)"} (Stata 9 and later) or
{opth "plot(plot_option:plot)"} (Stata 8 only)
provides a way to add other plots to the generated graph.


{title:Examples}

{p 4 4 2}Note: {cmd:anova} syntax is that introduced in Stata 11. 

{p 4 8 2}{cmd:. gen forxmpg = foreign * mpg}{p_end}
{p 4 8 2}{cmd:. regress price weight mpg forxmpg foreign}{p_end}
{p 4 8 2}{cmd:. rvfplot2}

{p 4 8 2}{cmd:. anova price rep foreign rep*foreign c.weight}{p_end}
{p 4 8 2}{cmd:. rvfplot2, rscale(sqrt(abs(X)))}

{p 4 8 2}{cmd:. glm price weight, link(log)}{p_end}
{p 4 8 2}{cmd:. rvfplot2, anscombe yli(0)}

{p 4 8 2}{cmd:. glm price weight, link(log)}{p_end}
{p 4 8 2}{cmd:. rvfplot2, anscombe yli(0) lowess}{p_end}
{p 4 8 2}{cmd:. rvfplot2, anscombe yli(0) lowess(bw(0.9))}


{title:Acknowledgment} 

{p 4 4 2}Phil Ender provided very helpful comments on a previous version. 


{title:Reference}     

{phang}McCullagh, P., and J. A. Nelder. 1989. 
{browse "http://www.stata.com/bookstore/glm.html":{it:Generalized Linear Models}. 2nd ed.}  London: Chapman & Hall/CRC. 


{title:Author}

{p 4 4 2}Nicholas J. Cox{break}
Durham University, U.K.{break} 
n.j.cox@durham.ac.uk


{title:Also see}

{psee}
Article: {it:Stata Journal}, volume 10, number 1: {browse "http://www.stata-journal.com/sjpdf.html?articlenum=up0027":gr0009_1},{break}
         {it:Stata Journal}, volume 4, number 4: {browse "http://www.stata-journal.com/sjpdf.html?articlenum=gr0009":gr0009}

{psee}
{space 2}Help:  {manhelp scatter G:graph twoway scatter}, {manhelp predict R}, {helpb modeldiag}
{p_end}
