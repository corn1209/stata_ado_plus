{smcl}
{* 26feb2003/3nov2004/4oct2007/18dec2009}{...}
{hline}
{cmd:help rdplot}{right:({browse "http://www.stata-journal.com/sjpdf.html?articlenum=up0027":SJ10-1: gr0009_1})}
{hline}

{title:Title}

{p 4 4 2}{hi:rdplot} {hline 2} Plot residual distribution after model fit


{title:Syntax}

{p 8 17 2}{cmd:rdplot}
[{it:plottype}]
[{cmd:,} {it:residualtype}
{cmdab:rsc:ale(}{it:exp}{cmd:)}
{cmd:by(}{it:varname}{cmd:)}
[{cmd:at(}{it:numlist}{cmd:)} {c |} {cmdab:g:roup(}{it:#}{cmd:)}] 
{it:graph_options}
[{cmd:addplot(}{it:plot}{cmd:)}
{c |}
{cmd:plot(}{it:plot}{cmd:)}]  
]


{title:Description}

{p 4 4 2}{cmd:rdplot} is for use after {cmd:regress} or similar commands; see
the help file for the command of interest. 

{p 4 4 2}{cmd:rdplot} graphs residual distributions. The residuals are, by
default, those calculated by {cmd:predict, residuals} or (if the previous
estimation command was {cmd:glm}) by {cmd: predict, response}. 

{p 4 4 2}The graph by default is a single or multiple dotplot, as produced
by {cmd:dotplot}. 

{p 4 4 2}Histograms or box plots may be selected by specifying either
{cmd:histogram}, {cmd:hbox}, or {cmd:box} as {it:plottype}.  

{p 4 4 2}Strip plots as implemented by {cmd:stripplot}, skewness plots as
implemented by {cmd:skewplot}, quantile plots as implemented by
{cmd:qplot}, or beam plots as implemented by {cmd:beamplot} may be selected 
by specifying {cmd:stripplot}, {cmd:skewplot}, {cmd:qplot}, or {cmd:beamplot} as {it:plottype}.  

{p 4 4 2}In the latter four cases, {cmd:stripplot} or {cmd:skewplot} or
{cmd:qplot} or {cmd:beamplot} must be installed first. Use {helpb findit} 
to find the location of the latest version in each case. 

{p 4 4 2}{it:plottype} may be abbreviated. {cmd:b} is interpreted 
as {cmd:box} (not as {cmd:beamplot}), and {cmd:s} is interpreted as
{cmd:stripplot} (not as {cmd:skewplot}). Thus be sure to give unambiguous abbreviations if you 
wish the latter types rather than the former. 


{title:Options}

{p 4 8 2}{it:residualtype} specifies a type of residual other than the default.
The following types are currently supported: {cmdab:a:nscombe},
{cmdab:d:eviance}, {cmdab:l:ikelihood}, {cmdab:p:earson}, {cmdab:r:esiduals},
{cmdab:resp:onse}, {cmdab:rsta:ndard}, {cmdab:rstu:dent}, {cmdab:s:core},
{cmdab:w:orking}. 

{p 4 8 2}{cmd:rscale(}{it:exp}{cmd:)} specifies a transformed scale on which to
show the residuals using Stata syntax and {cmd:X} as a placeholder for the
residual variable name. Thus {cmd:rscale(X^2)} specifies squaring, to show
relative contribution to residual variance; {cmd:rscale(abs(X))} specifies
absolute value, to set aside sign; {cmd:rscale(sqrt(abs(X)))} specifies root of
absolute value, a useful scale on which to check for heteroskedasticity. 

{p 4 8 2}{cmd:by(}{it:varname}{cmd:)} specifies that plots be 
given for groups of {it:varname}. If {cmd:at()} or {cmd:group()} is 
also specified, as below, groups are as specified by that option; otherwise, 
groups are the distinct values of {it:varname}. 

{p 4 8 2}{cmd:at(}{it:numlist}{cmd:)} or {cmd:group(}{it:#}{cmd:)} specifies
how groups of a classifying variable are to be determined.  If
{cmd:by(}{it:varname}{cmd:)} is specified, as above, {cmd:at()} or
{cmd:group()} is applied to that variable; otherwise, the first-named predictor
or covariate used in the previous regression-type command is used. {cmd:at()}
and {cmd:group()} are options of {cmd:egen, cut()}; see {helpb egen}. In
brief, {cmd:at()} specifies lower limits of user-defined classes, and
{cmd:group()} specifies the number of quantile-based groups with
(approximately) equal frequency to be used. (With {cmd:at()}, supply one high
value beyond the maximum of the data for best results.) 

{p 4 8 2}{it:graph_options} are any of the options allowed with whichever of 
{helpb dotplot}, {helpb histogram},
{helpb graph_box:graph hbox}, 
{helpb graph_box:graph box}, {helpb stripplot}, {helpb skewplot},
{helpb qplot}, or 
{helpb beamplot} is selected as {it:plottype}. 

{p 4 8 2}
{opth "addplot(addplot_option:plot)"} (Stata 9 and later) or
{opth "plot(plot_option:plot)"} (Stata 8 only)
provides a way to add other plots to the generated graph.
{cmd:addplot()} or {cmd:plot()} is 
allowed whenever {it:plottype} permits (and so not with {cmd:dotplot},
{cmd:box}, or {cmd:hbox}). 


{title:Examples}

{p 4 8 2}{cmd:. regress width length}{p_end}
{p 4 8 2}{cmd:. rdplot, g(3)}{p_end}
{p 4 8 2}{cmd:. foreach v in width length {c -(}}{p_end}
{p 4 8 2}{cmd:. {space 8}gen log`v' = log(`v')}{p_end}
{p 4 8 2}{cmd:. {c )-}} {p_end}
{p 4 8 2}{cmd:. regress logwidth loglength}{p_end}
{p 4 8 2}{cmd:. rdplot, g(3)}{p_end}
{p 4 8 2}{cmd:. rdplot b, g(3)}{p_end}
{p 4 8 2}{cmd:. rdplot hb, g(3)}{p_end}
{p 4 8 2}{cmd:. rdplot hi, g(3)}
	
{p 4 8 2}{cmd:. glm price weight, link(log)}{p_end}
{p 4 8 2}{cmd:. rdplot hi, anscombe yli(0) normal}

{p 4 8 2}{cmd:. logit foreign weight}{p_end}
{p 4 8 2}{cmd:. egen gweight = cut(weight), group(5)}{p_end}
{p 4 8 2}{cmd:. egen gweight_median = median(weight), by(gweight)}{p_end}
{p 4 8 2}{cmd:. rdplot stripplot, over(gweight_median) vertical}{p_end}
{p 4 8 2}{cmd:. rdplot stripplot, over(gweight_median) vertical box(barw(100))}


{title:Author}

{p 4 4 2}Nicholas J. Cox{break} 
Durham University, U.K.{break}
n.j.cox@durham.ac.uk


{title:Also see}

{psee}
Article: {it:Stata Journal}, volume 10, number 1: {browse "http://www.stata-journal.com/sjpdf.html?articlenum=up0027":gr0009_1},{break}
         {it:Stata Journal}, volume 4, number 4: {browse "http://www.stata-journal.com/sjpdf.html?articlenum=gr0009":gr0009}

{psee}
{space 2}Help:  {manhelp predict R}, {helpb modeldiag}
{p_end}
