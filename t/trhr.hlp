{smcl}
{cmd:help trhr}
{hline}

{title:Title}

{p2colset 5 14 21 2}{...}
{p2col :{hi: trhr} {hline 2}}Calculate the hazard ratios from the fitted threshold regression model{p_end}
{p2colreset}{...}

{title:Syntax}

{p 8 17 2}
{cmd:trhr} {ifin}{cmd:,} {cmd:var}({varname}) {cmd:scenario}(string) {cmd:timevalue}(numlist)  [{cmd:}{it:other options}] 

{synoptset 21 tabbed}{...}
{synopthdr}
{synoptline}
{syntab:Hazard ratio}
{synopt :{opth var(varname)}}specify the categorical variable for the calculation of hazard ratios{p_end}
{synopt :{opt sc:enario(string)}}specify a scenario (value settings of covariates) where the hazard ratios are calculated{p_end}
{synopt :{opt t:imevalue(numlist)}}specify values of time at which the hazard ratios are calculated{p_end}

{syntab:Graph}
{synopt :{opt graph(hz|sv|ds|hr)}}specify the type of curves to be generated{p_end}
{synopt :{opt graphopt(string)}}specify the graph settings{p_end}
{synopt :{opt y1}}add the horizontal line at y=1 in the hazard ratio plot{p_end}

{syntab:Confidence Interval}
{synopt :{opt ci}}calculate and display the bootstrap confidence interval of hazard ratios{p_end}
{synopt :{opt boot:strap(#)}}specify the number of replications in bootstrap, default is 2000{p_end}
{synopt :{opt l:evel(#)}}set confidence level of the bootstrap confidence intervals of hazard ratios; default is {cmd:level(95)}{p_end}

{syntab:Saving results}
{synopt :{opt k:eep}}save the calculated hazard ratios (and the bounds of confidence intervals if the ``ci'' option is used) {p_end}
{synopt :{opt pre:fix(string)}}specify a prefix string which will be attached to the names of the new variables generated by the {cmd:keep} option{p_end}

{synoptline}
{p2colreset}{...}
{p 4 6 2}


{title:Description}

{pstd}
{cmd:trhr} calculates hazard ratios from the threshold regression model fitted by the {cmd:stthreg} command. 


{title:Options for trhr}

{dlgtab:Hazard Ratio}

{phang}
{opth var(varname)} specifies the name of the categorical variable for the calculation of hazard ratios.

{phang}
{opth scenario(string)} this option must be used if any covariates other than the categorical variable specified in var() option are also included in the threshold regression model fitted by the {cmd:stthreg} command. scenario() option is to assign values to those covariates by equations, one by one and seperated by a blank space. scenario() option is to set up a scenario at which hazard ratios are calculated.

{phang}
{opth timevalue(numlist)} this option must be specified. This timevalue() option is to assign time point values at which hazard ratios are calculated.

{dlgtab:Graph}
{phang}
{opt graph(hz|sv|ds|hr)} specifies the type of curves to be generated. The ``hz'' option is to plot hazard function curves, the ``sv'' option is to plot survival function curves, the ``ds'' option is to plot density function curves, and the ``hr'' option is to plot hazard ratio tendency plot over the specified time points.

{phang}
{opt graphopt(string)} specifies the graph settings (titles, legends, etc). If this option is used, the default graph settings in the {cmd:trhr} command will be ignored and the styles (such as titles, legends, labels, etc) of the output graphs completely depend on the options provided in graphopt(string). Since the {cmd:line} command in STATA is used in the {cmd:trhr} command to plot curves, any options suitable for the {cmd:trhr} command can be used in the graphopt(string) option to produce desired graphic styles.

{phang}
{opt y1} is used to add a horizontal line at $y=1$ to the hazard ratio tendency plot, in order to compare the hazard ratio values with the value of 1 graphically.

{dlgtab: Confidence interval}
{phang}
{opt ci} is used to output the bootstrap percentile confidence interval of the estimated hazard ratio value. By default, 2,000 boostrap replications will be performed and the confidence level is 95%.

{phang}
{opt bootstrap(#)} specifies the number of boostrap replications. By default, 2,000 bootstrap replications will be performed.

{phang}
{opt level(#)} is used to set the confidence level of the bootstrap percentile confidence interval of the hazard ratio value; default is {cmd:level(95)}.


{dlgtab: Saving results}

{phang}
{opt keep} is used to save the estimated hazard ratio values of specified time points in the current dataset. The specified time values are saved under a new variable with name ``hr_t''; the hazard ratio values are saved under a new variable with name headed with ``hr'' and tailed with the name of the corresponding dummy variable. If the ``ci'' option is used, then the lower and upper limit values of the confidence intervals will also be saved under two new variables with names headed with ``hrll'' and ``hrul'' respectively and tailed with the name of the corresponding dummy variable.

{phang}
{opt prefix(string)} specifies a string that will be attached to the head of the names of the new variables generated by the ``keep'' option.

 
{title:Example of trhr}
 
{pstd}Setup{p_end}
{phang2}{cmd:. use melanoma, clear}{p_end}

{pstd}Stset the data{p_end}
{phang2}{cmd:. stset survtime, failure(status)}{p_end}

{pstd}Fit threshold regression model by the stthreg command{p_end}
{phang2}{cmd:. xi: stthreg, lny0(sex ecells thick age i.ulcerat) mu(sex ecells thick age i.ulcerat)}{p_end}

{pstd}Calculate the hazard ratio for the ``ulcerat'' variable for the specified scenario at time point 4000 (in days){p_end}
{phang2}{cmd:. trhr, var(ulcerat) timevalue(4000) scenario(sex=0 ecells=0 thick=12 age=40)}{p_end}





